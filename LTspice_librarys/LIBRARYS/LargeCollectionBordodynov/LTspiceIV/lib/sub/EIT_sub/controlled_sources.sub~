
* Selfmade model of the LM334 from National Semiconductor or ST
* Helmut Sennewald
*
* Pin order: v+  v-  R
.SUBCKT LM334 v+ v- R
Q4 N002 N001 v+ 0 pnp1
Q5 N003 N001 v+ 0 pnp1
Q6 R N001 v+ 0 pnp1 14
Q1 N003 N003 R 0 npn1 14
Q2 N002 N003 N004 0 npn1
Q3 N001 N002 R 0 npn1
C1 N002 N004 50p
R2 N004 v- 1Âµ
R8 v+ v- 1G
C2 v+ v- 10p
C3 N003 v- 1p
C4 R v- 1p
.model npn1 NPN(Is=1e-15 BF=200 TF=1e-9 Cjc=0.5e-12 Cje=1e-12 VAF=100 Rb=100 Re=5)
.model pnp1 PNP(Is=1e-15 BF=100 TF=1e-7 Cjc=0.5e-12 Cje=1e-12 VAF=100 Rb=100 Re=5)
.ENDS

*current controlled Current-Source
*
.subckt crtl_ii 1 2 3 4 
F1 2 1 V1 {F}
V1 3 4 DC=0
.param F=1
.ends
*
*current controlled Voltage-Source
*
.subckt crtl_iv 1 2 3 4 
H1 1 2 V1 {H}
V1 3 4 DC=0
.param H=1k
.ends
*
*
*ideal Transformer
.subckt ideal_transformer i1 i2 o1 o2 
* Beispiel von LT ideal Transformer
* interner Knoten N001
R1 N001 0 1G ; Dummy-Widerstand
G3 i1 i2 N001 0 1 
G4 o2 o1 N001 0 {N} 
G1 0 N001 i1 i2 1 
G2 0 N001 o2 o1 {N} 
.param N=1
.ends ideal_transformer
*
