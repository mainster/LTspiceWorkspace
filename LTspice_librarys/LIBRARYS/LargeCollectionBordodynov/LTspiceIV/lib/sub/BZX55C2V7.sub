********************************
* Copyright:                   *
* Vishay Intertechnology, Inc. *
********************************
* Technology:	DISCRETE DEVICE
* Device:	Zener Diode BZX 55C 2V7
* Type:	Typical (nom)
* Remarks:	Macro model
* Revision:	
* Simulator:	PSPICE

.SUBCKT BZX55C2V7 a c

 DF a c DFOR
 DR c a DREV
 DB b a DBRE
 EB c b POLY(1) d 0 1.6 1
 IB 0 d 1m
 RB 0 d 1k TC=-0.1m

.MODEL DFOR D
 + IS =   12p  RS =    0  N  =  1.5  CJO= 300p
 + VJ = 500m  M  = 345m  FC = 700m  XTI=    3
 + EG =    1

.MODEL DREV D
 + IS = 150f  N  =   10  XTI=    3  EG =1.186

.MODEL DBRE D
 + IS =  12f  RS =   10  N  =    1  XTI=    0
 + EG =1.186

.ENDS BZX55C2V7

