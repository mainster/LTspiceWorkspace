***
*
* Dioden
*
***

.subckt Diode a k params: is=1e-15 n=1 cjo=4p m=0.5 vj=0.5
D1 a k DiodeGen
.model DiodeGen D(Is={is} N={n} Cjo={cjo} M={m} Vj={vj})
.ends

.model 1N4001	D(Is=14.11n N=1.984 Rs=33.89m Ikf=94.81 Xti=3 Eg=1.11
+		Cjo=25.89p M=.44 Vj=.3245 Fc=.5 Bv=75 Ibv=10u Tt=5.7u)

.model 1N4148   D(Is=2.682n N=1.836 Rs=.5664 Ikf=44.17m Xti=3 Eg=1.11 Cjo=4p
+               M=.3333 Vj=.5 Fc=.5 Isr=1.565n Nr=2 Bv=100 Ibv=100u Tt=11.54n)

.model BAS40    D(IS=8N RS=7.8 N=1.04 XTI=1.8 EG=.68 ISR=5n NR=1k IKF=10m
+               CJO=3P M=.42 VJ=.4 FC=.5 BV=40 IBV=100n TT=25P)

.model ZD47     D(Is=1E-15 Rs=.1 Ikf=0 N=1 Xti=3 Eg=1.11 Cjo=50p M=.5
+		Vj=.75 Fc=.5 Isr=1n Nr=2 Bv=4.7 Ibv=20m Nbv=1.6
+		Ibvl=2m Nbvl=15 Tbv1=-20u)

.model ZD69     D(Is=1E-15 Rs=.1 Ikf=0 N=1 Xti=3 Eg=1.11 Cjo=50p M=.5
+		Vj=.75 Fc=.5 Isr=1n Nr=2 Bv=6.9 Ibv=20m Nbv=1.6
+		Ibvl=2m Nbvl=15 Tbv1=-20u)


***
*
* Kleinsignal-Transistoren
*
***

.subckt Transistor c b e params: is=1e-15 bn=100 bi=1
Q1 c b e TrGen
.model   TrGen  NPN(Is={is} Bf={bn} Br={bi}) 
.ends 

.model BC547B   NPN(Is=7.049f Xti=3 Eg=1.11 Vaf=62.79 Bf=374.6 Ise=68f Ne=1.576
+               Ikf=81.57m Nk=.4767 Xtb=1.5 Br=1 Isc=12.4f Nc=1.835 Ikr=3.924
+               Rc=.9747 Cjc=5.25p Mjc=.3147 Vjc=.5697 Fc=.5 Cje=11.5p
+               Mje=.6715 Vje=.5 Tr=10n Tf=410.2p Itf=1.491 Xtf=40.06 Vtf=10)

.model BC557B   PNP(Is=1.02f Xti=3 Eg=1.11 Vaf=52.31 Bf=306.5 Ise=10.27f
+               Ne=1.764 Ikf=91.85m Nk=.5351 Xtb=1.5 Br=6.48 Isc=1.472f
+               Nc=1.294 Ikr=.5584 Rc=1.086 Cjc=9.81p Mjc=.332 Vjc=.4865 Fc=.5
+               Cje=30p Mje=.3333 Vje=.5 Tr=10n Tf=611.6p Itf=1.373 Xtf=26.05
+               Vtf=10)

.model Q2N2222	NPN(Is=14.34f Xti=3 Eg=1.11 Vaf=74.03 Bf=255.9 Ne=1.307
+		Ise=14.34f Ikf=.2847 Xtb=1.5 Br=6.092 Nc=2 Isc=0 Ikr=0 Rc=1
+		Cjc=7.306p Mjc=.3416 Vjc=.75 Fc=.5 Cje=22.01p Mje=.377 Vje=.75
+		Tr=46.91n Tf=411.1p Itf=.6 Vtf=1.7 Xtf=3 Rb=10)

.model Q2N2907	PNP(Is=650.6E-18 Xti=3 Eg=1.11 Vaf=115.7 Bf=231.7 Ne=1.829
+		Ise=54.81f Ikf=1.079 Xtb=1.5 Br=3.563 Nc=2 Isc=0 Ikr=0 Rc=.715
+		Cjc=14.76p Mjc=.5383 Vjc=.75 Fc=.5 Cje=19.82p Mje=.3357 Vje=.75
+		Tr=111.3n Tf=603.7p Itf=.65 Vtf=5 Xtf=1.7 Rb=10)


***
*
* Leistungstransitoren
*
***

.model BD239  NPN(Is=2.447p Xti=3 Eg=1.11 Vaf=100 Bf=208.2 Ise=70.69p
+               Ne=1.565 Ikf=.9743 Nk=.6134 Xtb=1.5 Br=12.59 Isc=11.68n
+               Nc=1.835 Ikr=3.86 Rc=.4685 Cjc=142p Mjc=.4353 Vjc=.75 Fc=.5
+               Cje=188.5p Mje=.4878 Vje=.75 Tr=194.2n Tf=19.85n Itf=164.1
+               Xtf=5.945 Vtf=10 Rb=.1)

.model BD240  PNP(Is=51.23f Xti=3 Eg=1.11 Vaf=100 Bf=434.1 Ise=51.23f Ne=1.22
+               Ikf=.3883 Nk=.5544 Xtb=2.2 Br=55.47 Isc=51.23f Nc=1.205
+               Ikr=10.87 Rc=.3443 Cjc=136.9p Mjc=.3155 Vjc=.75 Fc=.5
+               Cje=179.9p Mje=.4294 Vje=.75 Tr=20.25n Tf=13.05n Itf=6.85
+               Xtf=1.573 Vtf=10 Rb=.1)


***
*
* Darlington-Transistoren
*
***

.subckt D-NN c b e params: R=1e9 M1=1 M2=2
Q1 c b e1  D42C1 {M1}
Q2 c e1 e  D42C1 {M2}
R1 e1 e    {R}
.ends

.subckt D-PP c b e params: R=1e9 M1=1 M2=2
Q1 c b e1  D43C1 {M1}
Q2 c e1 e  D43C1 {M2}
R1 e1 e    {R}
.ends

.subckt D-PN c b e params: R=1e9 M1=1 M2=2
Q1 c1 b e  D43C1 {M1}
Q2 e c1 c  D42C1 {M2}
R1 c1 c    {R}
.ends

.model D42C1	NPN(Is=100f Xti=3 Eg=1.11 Vaf=100 Bf=100 Ise=100f
+		Ne=1.3 Ikf=3 Nk=.6 Xtb=1.5 Br=4 Isc=30p
+		Nc=1.5 Ikr=0.1 Rc=.1 Cjc=200p Mjc=.3333 Vjc=.5 Fc=.5
+		Cje=500p Mje=.3333 Vje=.5 Tr=300n Tf=1.5n Itf=6K
+		Xtf=35K Vtf=10)

.model D43C1	PNP(Is=100f Xti=3 Eg=1.11 Vaf=100 Bf=50 Ise=80f
+		Ne=1.2 Ikf=1 Nk=.5 Xtb=1.5 Br=8 Isc=1p Nc=1.6
+		Ikr=0.07 Rc=.12 Cjc=220p Mjc=.3333 Vjc=.5 Fc=.5 Cje=600p
+		Mje=.3333 Vje=.5 Tr=400n Tf=2n Itf=10K Xtf=200K
+		Vtf=10)


***
*
* HF-Transistoren
*
***

.SUBCKT BFR93 c b e
Lbo  b  b1  0.5n
Lbi  b1 b2  0.85n
Leo  e e1   0.6n
Lei  e1 e2  0.7n
Lc   c  c1  0.5n
Cbe  b1 e1  73f
Cbc  b1 c1  84f
Cce  c1 e1  165f
Q1   c1 b2 e2  BFR93X
.MODEL BFR93X NPN
+ IS=8.67f   BF=138    BR=59     EG=1.11
+ VAF=20     VAR=27    NF=0.936  NR=0.888
+ IKF=0.33   IKR=15m   TR=1.1n
+ ISE=2.62p  NE=1.55   TF=33.4p  ITF=2.5m
+ ISC=0.71f  NC=1.95   VTF=0.18  XTF=0.283
+ RBM=3.5    RB=7.2    RE=1      RC=0.13
+ IRB=44u    XTB=0     XTI=3
+ CJE=3f     MJE=0.5   VJE=0.7   FC=0.76
+ CJC=1p     MJC=0.35  VJC=0.73  XCJC=0.21
+ CJS=0      MJS=0.33  VJS=0.75
.ENDS

.SUBCKT BFT93 c b e
Lbo  b  b1  0.5n
Lbi  b1 b2  0.85n
Leo  e e1   0.6n
Lei  e1 e2  0.7n
Lc   c  c1  0.5n
Cbe  b1 e1  73f
Cbc  b1 c1  84f
Cce  c1 e1  165f
Q1   c1 b2 e2  BFT93X
.MODEL BFT93X PNP
+ IS=4.54f   BF=99     BR=10     EG=1.11
+ VAF=11     VAR=48    NF=0.91   NR=1.27
+ IKF=16m    IKR=20m   TR=2.1n
+ ISE=12.2f  NE=1.12   TF=32.2p  ITF=13u
+ ISC=0.02f  NC=1.21   VTF=0.21  XTF=0.3
+ RBM=1.6    RB=8      RE=1.5    RC=0.67
+ IRB=0.8m   XTB=0     XTI=3
+ CJE=1.8    MJE=0.32  VJE=0.79  FC=0.75
+ CJC=0.92p  MJC=0.3   VJC=1.2   XCJC=0.3
+ CJS=0      MJS=0.33  VJS=0.75
.ENDS

.SUBCKT BFP405 c b e
Lbo  b  b1  0.53n
Lbi  b1 b2  0.47n
Leo  e e1   0.05n
Lei  e1 e2  0.23n
Lco  c  c1  0.58n
Lci  c1 c2  0.56n
Cbe  b1 e1  136f
Cbc  b1 c1  6.9f
Cce  c1 e1  134f
Q1   c2 b2 e2  BFP405X
.MODEL BFP405X NPN
+ IS=9.21f   BF=83     BR=10.5   EG=1.11
+ VAF=39     VAR=34    NF=1.04   NR=0.97
+ IKF=0.165  IKR=0.25  TR=1.5n
+ ISE=15.8f  NE=1.78   TF=4.59p  ITF=1.34m
+ ISC=0.04f  NC=1.32   VTF=0.2   XTF=0.364
+ RBM=1.35   RB=15     RE=1.93   RC=0.127
+ IRB=0.21m  XTB=0     XTI=3
+ CJE=3.73f  MJE=0.38  VJE=0.7   FC=0.998
+ CJC=97f    MJC=0.49  VJC=1     XCJC=0.08
+ CJS=0      MJS=0.33  VJS=0.75
.ENDS


***
*
* MOSFETs
*
***

.subckt nmosfet 1 2 3 4 params: k=4m ua=50 uth=1 uinv=0.6 gamma=0.5
+                               rg=25 cgs=4.4p cgd=2p cbd=3p cbs=3p
rg  2 2a     {rg}
m1  1 2a 3 4 nmosfet
cgs 2a 3     {cgs}
cgd 2a 1     {cgd}
cbd 1 4      {cbd}
cbs 3 4      {cbs}
.model nmosfet nmos( kp={k} vto={uth} phi={uinv} gamma={gamma} lambda={1/ua} )
.ends

.subckt pmosfet 1 2 3 4 params: k=4m ua=50 uth=-1 uinv=0.6 gamma=0.5
+                               rg=25 cgs=4.4p cgd=2p cbd=3p cbs=3p
rg  2 2a     {rg}
m1  1 2a 3 4 pmosfet
cgs 2a 3     {cgs}
cgd 2a 1     {cgd}
cbd 1 4      {cbd}
cbs 3 4      {cbs}
.model pmosfet pmos( kp={k} vto={uth} phi={uinv} gamma={gamma} lambda={1/ua} )
.ends


***
*
* Sperrschicht-FET
*
***

.MODEL  BF245B   NJF
+             VTO = -2.3085E+000
+            BETA = 1.09045E-003
+          LAMBDA = 2.31754E-002
+              RD = 7.77648E+000
+              RS = 7.77648E+000
+              IS = 2.59121E-016
+            CGS  = 2.00000E-012
+            CGD  = 2.20000E-012
+              PB = 9.91494E-001
+              FC = 5.00000E-001


***
*
* DMOS-FETs
*
***

.SUBCKT BSS92 1 2 3
LS 5 2 7N
LD 97 3 5N
RG 86 87 5.5M
RS 5 76 886M
D92 97 76 DREV
.MODEL DREV D CJO=0.05N RS=20M TT=35N IS=300P BV=240
M92 98 87 76 76 MBUZ
.MODEL MBUZ PMOS VTO=-1.489 KP=0.049
M2 87 98 8 8 MSW
.MODEL MSW PMOS VTO=-0.001 KP=5
M3 98 87 8 8 MSW
COX 87 8 0.15N
DGD 98 8 DCGD
.MODEL DCGD D CJO=120P M=0601 VJ=0.47
CGS 76 87 0.06N
MHELP 98 98 97 98 MVRD
.MODEL MVRD PMOS VTO=20.07 KP=0.008
LG 86 1 7N
.ENDS

.SUBCKT BSS101 1 2 3
LS 5 2 7N
LD 95 3 5N
RG 4 11 5.5M
RS 5 76 213M
D101 76 95 DREV
.MODEL DREV D CJO=0.017N RS=20M TT=20N IS=300P BV=240
M101 86 11 76 76 MBUZ
.MODEL MBUZ NMOS VTO=1.448 KP=0.104
M2 11 86 8 8 MSW
.MODEL MSW NMOS VTO=0.001 KP=5
M3 86 11 8 8 MSW
COX 11 8 0.09N
DGD 8 86 DCGD
.MODEL DCGD D CJO=0.012N M=0.383 VJ=0.948
CGS 76 11 0.055N
MRDR 86 86 95 86 MVRD
.MODEL MVRD NMOS VTO=-9.707 KP=.015
LG 4 1 7N
.ENDS

.SUBCKT BSS129 1 2 3
LS 5 2 7N
LD 83 3 5N
RG 4 11 5
RS 5 76 1M
DBSS129 76 83 DREV
.MODEL DREV D CJO=30P RS=20M TT=50N IS=30P BV=240
MBSS129 90 11 76 76 MBUZ
.MODEL MBUZ NMOS VTO=-1.30 KP=0.165
M2 11 90 8 8 MSW
.MODEL MSW NMOS VTO=5 KP=25
M3 90 11 8 8 MSW
COX 11 8 66P
DGD 8 90 DCGD
.MODEL DCGD D CJO=13.57P M=0.475 VJ=1.062
CGS 76 11 25P
RDR 90 83 2
LG 4 1 7N
.ENDS

.model BSD215   NMOS(Level=3 Gamma=0 Delta=0 Eta=0 Theta=0 Kappa=0 Vmax=0
+               Xj=0 Tox=100n Uo=600 Phi=.6 Kp=20.85u W=540u L=2u Rs=20m
+               Vto=.95 Rd=25 Cbd=5.352p Pb=.8 Mj=.5 Fc=.5 Cgso=1.2p
+               Cgdo=1.2p Rg=0 Is=124.9p N=1.189 Rb=366.6)

.model IRF142	NMOS(Level=3 Gamma=0 Delta=0 Eta=0 Theta=0 Kappa=0.2 Vmax=0 Xj=0
+		Tox=100n Uo=600 Phi=.6 Rs=21.94m Kp=20.61u W=.97 L=2u Vto=3.189
+		Rd=22.19m Cbd=2.398n Pb=.8 Mj=.5 Fc=.5 Cgso=1.113n
+		Cgdo=432.1p Rg=5.659 Is=1.303p N=1 Tt=142n)

.model IRF9142	PMOS(Level=3 Gamma=0 Delta=0 Eta=0 Theta=0 Kappa=0.2 Vmax=0 Xj=0
+		Tox=100n Uo=300 Phi=.6 Rs=70.6m Kp=10.15u W=1.9 L=2u Vto=-3.67
+		Rd=60.66m Cbd=2.141n Pb=.8 Mj=.5 Fc=.5 Cgso=877.2p
+		Cgdo=369.3p Rg=.811 Is=52.23E-18 N=2 Tt=140n)


***
*
* Integrierte Transistoren
*
***

.MODEL N1  NPN
+ IS=1f    BF=110    BR=10   ISE=200f NE=2
+ VAF=100  VAR=10    IKF=5m  IKR=10m
+ CJC=0.2p MJC=0.333 VJC=0.6 FC=0.8
+ CJE=0.1p MJE=0.5   VJE=0.8
+ CJS=1p   MJS=0.333 VJS=0.6
+ RB=500   RBM=100   RC=100  RE=10
+ TF=100p  TR=500p   ITF=10m VTF=5

.MODEL P1  PNP
+ IS=0.5f  BF=55     BR=10   ISE=200f NE=2
+ VAF=50   VAR=5     IKF=5m  IKR=10m
+ CJC=0.5p MJC=0.333 VJC=0.6 FC=0.8
+ CJE=0.1p MJE=0.5   VJE=0.8
+ CJS=2p   MJS=0.333 VJS=0.6
+ RB=200   RBM=50    RC=100  RE=10
+ TF=150p  TR=500p   ITF=10m VTF=5

.MODEL Schottky D
+ IS=1n    CJO=0.2p  M=0.333 VJ=0.4
+ EG=0.6   FC=0.5    TT=10P  RS=100

.subckt N1-Schottky c b e s params: A=1
Q1   c1 b1 e [s]  N1s {A}
G1   b1 c1        value = { 1e-9*A*exp(v(b1,c1)/0.026) }
Rb1  b  b1        {100/A}
Rc1  c  c1        {100/A}
.MODEL N1s NPN
+ IS=1f    BF=55     BR=10   ISE=200f NE=2
+ VAF=100  VAR=10    IKF=5m  IKR=10m
+ CJC=0.1p MJC=0.333 VJC=0.6 FC=0.8
+ CJE=0.1p MJE=0.5   VJE=0.8
+ CJS=0.5p MJS=0.333 VJS=0.6
+ RB=400   RBM=0.1   RC=0.1  RE=10
+ TF=100p  TR=500p   ITF=10m VTF=5
.ends

.subckt nmos d g s b params: wu=3 lu=3
M1 d g s b NMOS W={wu*1e-6}
+               L={lu*1e-6}
+               AS={(wu+2)*5e-12}
+               AD={(wu+2)*5e-12}
+               PS={(2*wu+14)*1e-6}
+               PD={(2*wu+14)*1e-6}
+               NRS={3/wu}
+               NRG={3/wu}
+               M=1
.MODEL NMOS NMOS
+ LEVEL=1 KP=30u VTO=1 GAMMA=0.5 LAMBDA=0.02
+ PHI=0.6 TOX=57.5n UO=500 CJ=0.2m MJ=0.5 PB=0.8
+ JS=10u RSH=25 CGSO=500p CGDO=500p
.ends

.subckt pmos d g s b params: wu=7.5 lu=3
M1 d g s b PMOS W={wu*1e-6}
+               L={lu*1e-6}
+               AS={(wu+2)*5e-12}
+               AD={(wu+2)*5e-12}
+               PS={(2*wu+14)*1e-6}
+               PD={(2*wu+14)*1e-6}
+               NRS={3/wu}
+               NRG={3/wu}
+               M=1
.MODEL PMOS PMOS
+ LEVEL=1 VTO=-1 KP=12u GAMMA=0.5 LAMBDA=0.033
+ PHI=0.6 TOX=57.5n UO=200 CJ=0.2m MJ=0.5 PB=0.8
+ JS=20u RSH=50 CGSO=500p CGDO=500p
.ends

.subckt nmosd d g s b params: wu=3 lu=3 uth=-1 gamma=0.8
M1 d g s b NMOSD W={wu*1e-6}
+                L={lu*1e-6}
+                AS={(wu+2)*5e-12}
+                AD={(wu+2)*5e-12}
+                PS={(2*wu+14)*1e-6}
+                PD={(2*wu+14)*1e-6}
+                NRS={3/wu}
+                NRG={3/wu}
+                M=1
.MODEL NMOSD NMOS
+ LEVEL=2 VTO={Uth} KP=30u GAMMA={gamma} LAMBDA=0.02
+ TOX=57.5n UO=500 CJ=0.2m MJ=0.5 PB=0.8 PHI=0.6
+ JS=10u RSH=25 CGSO=500p CGDO=500p
.ends

.subckt pmosd d g s b params: wu=7.5 lu=3 uth=1 gamma=0.8
M1 d g s b PMOSD W={wu*1e-6}
+                L={lu*1e-6}
+                AS={(wu+2)*5e-12}
+                AD={(wu+2)*5e-12}
+                PS={(2*wu+14)*1e-6}
+                PD={(2*wu+14)*1e-6}
+                NRS={3/wu}
+                NRG={3/wu}
+                M=1
.MODEL PMOSD PMOS
+ LEVEL=2 VTO={Uth} KP=12u GAMMA={gamma} LAMBDA=0.033
+ TOX=57.5n UO=200 CJ=0.2m MJ=0.5 PB=0.8 PHI=0.6
+ JS=20u RSH=50 CGSO=500p CGDO=500p
.ends

.subckt nmos2 d g s b params: wu=2 lu=1.2
M1 d g s b nmos2   W={wu*1e-6}
+                  L={lu*1e-6}
+                  AS={wu*2.5e-12}
+                  AD={wu*2.5e-12}
+                  PS={(2*wu+5)*1e-6}
+                  PD={(2*wu+5)*1e-6}
+                  NRS={1/wu}
+                  NRG={1/wu}
+                  M=1
.model nmos2 nmos ( level=2
+ vto=0.73 js=10u pb=0.9
+ cgso=300p cgdo=300p cgbo=150p rsh=25
+ cj=0.36m mj=0.4 cjsw=250p mjsw=0.2
+ tox=25n nsub=3e16 nfs=5e11
+ xj=0.18u ld=0 wd=0.4u
+ uo=500 ucrit=0.3e6 uexp=0.25
+ vmax=0.07e6 neff=5 delta=0.3
+ kf=1e-26 af=1.3 )
.ends

.subckt pmos2 d g s b params: wu=6 lu=1.2
M1 d g s b pmos2   W={wu*1e-6}
+                  L={lu*1e-6}
+                  AS={wu*2.5e-12}
+                  AD={wu*2.5e-12}
+                  PS={(2*wu+5)*1e-6}
+                  PD={(2*wu+5)*1e-6}
+                  NRS={1/wu}
+                  NRG={1/wu}
+                  M=1
.model pmos2 pmos ( level=2
+ vto=-0.75 js=20u pb=0.9
+ cgso=300p cgdo=300p cgbo=150p rsh=45
+ cj=0.34m mj=0.5 cjsw=220p mjsw=0.2
+ tox=25n nsub=1.8e16 nfs=1.3e12
+ xj=0.06u ld=0 wd=0.4u
+ uo=170 ucrit=0.2e6 uexp=0.25
+ vmax=0.05e6 neff=3 delta=0.8
+ kf=5e-28 af=1.3 )
.ends

.subckt n-mesfet d g s params: w=1m uth=-2 kw=100 cgw=2 rgmin=1 rgw=1.5m
+                              rsmin=0.2 rsw=0.5m
RG g g1   { rgmin + rgw/w }
J1 d g1 s nmesfet
.model nmesfet NJF
+    VTO = { uth }
+   BETA = { kw*w/2 }
+ LAMBDA = 0.05
+     RD = { rsmin + rsw/w }
+     RS = { rsmin + rsw/w }
+     IS = { 1u * w }
+    CGS = { cgw * w }
+    CGD = { cgw * w }
+     PB = 1
+     FC = 0.5
.ends

.MODEL UHFP-N NPN
+   IS= 1.905187e-16    BF= 1.045586e+02   NF= 1.000000e+00  VAF= 7.200000e+01
+  IKF= 5.412000e-02   ISE= 1.743487e-19   NE= 1.400000e+00   BR= 1.000000e+01
+   NR= 1.000000e+00   VAR= 4.500000e+00  IKR= 5.412000e-02  ISC= 1.905187e-14
+   NC= 1.800000e+00    RB= 5.033198e+01  RBM= 1.977632e+00
+   RE= 1.847746e+00    RC= 1.151193e+01  VJE= 8.720000e-01  CJE= 4.700000e-13
+  MJE= 5.100000e-01    TF= 6.500000e-12
+  PTF= 0.000000e+00   VJC= 9.700000e-01  CJC= 3.984710e-13
+  MJC= 2.400000e-01  XCJC= 9.000000e-01   TR= 4.000000e-09  CJS= 1.688846e-13
+  VJS= 1.000000e+00   MJS= 0.000000e+00
+   KF= 0.000000e+00    AF= 1.000000e+00   FC= 5.000000e-01

.MODEL UHFP-P PNP
+   IS= 1.036422e-16    BF= 5.227930e+01   NF= 1.000000e+00  VAF= 3.000000e+01
+  IKF= 5.412000e-02   ISE= 9.484570e-20   NE= 1.400000e+00   BR= 7.000000e+00
+   NR= 1.000000e+00   VAR= 4.500000e+00  IKR= 5.412000e-02  ISC= 1.036422e-14
+   NC= 1.800000e+00    RB= 2.458813e+01  RBM= 7.910526e-01
+   RE= 1.847746e+00    RC= 3.367177e+01  VJE= 8.800000e-01  CJE= 4.700000e-13
+  MJE= 5.700000e-01    TF= 9.000000e-12
+  PTF= 0.000000e+00   VJC= 1.230000e+00  CJC= 5.543081e-13
+  MJC= 3.000000e-01  XCJC= 9.000000e-01   TR= 4.000000e-09  CJS= 1.688846e-13
+  VJS= 1.000000e+00   MJS= 0.000000e+00
+   KF= 0.000000e+00    AF= 1.000000e+00   FC= 5.000000e-01


***
*
* Modelle fuer VV-OP
*
***

.subckt vvkomp up un ua k1 k2 params: R1=1k R2=1k Ra=100
+ C1=10u C2=10p S1=1 S2=1 F3=1e9
r_rd	up un	1e12
r_r1	k1 0	{R1}
c_c1	k1 0	{C1}
r_r2	k2 0	{R2}
c_c2	k2 0	{C2}
g_g2	k2 0	value = { V(k1) * S2 }
g_g1	k1 0	value = { V(up,un) * S1 }
r_ra	k5 ua	{Ra}
e_e2	k5 0	value = { V(k4) }
c_c3	k4 0	{0.159/F3}
r_r3	k3 k4	1
e_e1	k3 0	value = { V(k2) }
r_rx	k3 0	1e9
.ends

.subckt vvint up un ua params: ad=1e5 ft=1e6 ra=1k
r_rd    up un	1e12
g_g1	0 k1	value = { v(up,un) }
c_c1	k1 0	{ 0.159/ft }
r_r1    k1 0    { ad }
e_e1	k2 0	value = { v(k1) }
r_ra	k2 ua	{ra}
.ends

.subckt vv up un ua params: ad=1e5 ft=1e6 sr=1 ra=100 uamax=4 uamin=-4 iamax=10m
r_rd    up un	1e12
g_g1	0 k1	table { (v(up,un)-v(k1)/ad)*6.28e-6*ft/sr } = (-1,-1) (1,1)
c_c1	k1 0	{ 1e-6/sr }
r_r1    k1 0    1e12
g_g2	k1 0	value = { limit(exp((v(ua)-uamax)/5m) - exp((uamin-v(ua))/5m),-1e12,1e12) }
e_e1	k2 0	value = { v(k1) }
g_ra	k2 ua	value = { limit(v(k2,ua)/ra,-iamax,iamax) }
r_ram   k2 ua   1e6
r_ral	ua 0    1e12
.ends


***
*
* Modelle fuer VC-/CV/CC-OP
*
***

.subckt vc up un ua params: s1=100m r=10k c=5p uamin=-4 uamax=4
r_rd    up un   1e12
g_g1	ua 0	value = { s1*v(un,up) }
c_c1	ua 0	{c}
r_r1	ua 0	{r}
g_g2    ua 0	value = { limit(exp((v(ua)-uamax)/5m) - exp((uamin-v(ua))/5m),-1e12,1e12) }
.ends

.subckt cv up un ua params: rs=10 r=10k c=5p ra=100 uamin=-4 uamax=4 iamax=10m
r_re	up 0	1e12
e_e1	k1 0 	value = { v(up) }
r_rs    k1 un	{rs}
g_g1    k2 0	value = { v(un,k1)/rs }
r_r1    k2 0	{r}
c_c1    k2 0	{c}
g_g2    k2 0	value = { limit(exp((v(ua)-uamax)/5m) - exp((uamin-v(ua))/5m),-1e12,1e12) }
e_e2    k3 0	value = { v(k2) }
g_ra	k3 ua   value = { limit(v(k3,ua)/ra,-iamax,iamax) }
r_ram   k3 ua   1e6
r_ral	ua 0	1e12
.ends

.subckt cc up un ua params: rs=10 r=10k c=5p uamin=-4 uamax=4
r_re    up 0	1e12
e_e1	k1 0	value = { v(up) }
r_rs    k1 un	{rs}
g_g1    ua 0	value = { v(un,k1)/rs }
r_r1    ua 0	{r}
c_c1    ua 0	{c}
g_g2    ua 0	value = { limit(exp((v(ua)-uamax)/5m) - exp((uamin-v(ua))/5m),-1e12,1e12) }
.ends


***
*
* Widerstand ohne Rauschen
*
***

.subckt ror 1 2 params: R=1k
g_g1    1 2     value = { v(1,2)/R }
r_r1    1 2     1e12
.ends


***
*
* Modelle fuer Nachrichtentechnik
*
***

% Verstärker-Modell mit Rauschquellen
.subckt ver e a params: re=50 ra=50 a=10 ar=0 ur=0 ir=0
r_ur  1 0       {2*6.04e19*ur*ur}
r_ux  1 0       {2*6.04e19*ur*ur}
r_ir  2 0       {2*6.04e19*ir*ir}
r_ix  2 0       {2*6.04e19*ir*ir}
e_ur  e e1 1 0  1
g_ir  0 e1 0 2  1
g_re  e1 e2     value={v(e1,e2)/re}
e_ar  e2 0      value={ar*v(a,0)}
e_a   a1 0      value={a*v(e,0)}
g_ra  a1 a      value={v(a1,a)/ra}
r_rd  a 0       1e12
.ends

% Wellen-Messer
.subckt wellen e a f r params: z0=50
v_i  e a  dc 0
r_e  e 0  1e12
r_a  a 0  1e12
e_f  f 0  value={0.5*(v(e,0)/sqrt(z0)+i(v_i)*sqrt(z0))}
e_r  r 0  value={0.5*(v(e,0)/sqrt(z0)-i(v_i)*sqrt(z0))}
r_f  f 0  1
r_r  r 0  1
.ends

% Port fuer S-Parameter-Messung
.subckt sport g f r params: z0=50 actport=1 port=1
v_ug  1 0   ac {if(port==actport,1,0)}
r_rg  1 g   {z0}
r_rd  0 g   1e12
e_f   f 0   value={0.5*(v(g,0)+v(1,g))/sqrt(z0)}
e_r   r 0   value={0.5*(v(g,0)-v(1,g))/sqrt(z0)}
r_f   f 0   1
r_r   r 0   1
.ends

% Loop Gain
.subckt lg v1 v2 i1 i2 params: lgm=1
v_1   i v1  dc 0 ac {if(lgm==1,1,0)}
v_2   i v2  dc 0
i_i   i 0   dc 0 ac {if(lgm==1,0,1)}
h_1   i1 0  v_1 1
h_2   i2 0  v_2 1
r_1   i1 0  1
r_2   i2 0  1
.ends
.subckt lgdiff v1 v2 v3 v4 i1 i2 params: lgm=1
v_1   i v1  dc 0 ac {if(lgm==1,1,0)}
v_2   i v2  dc 0
v_3   v3 v4 dc 0 ac {if(lgm==1,1,0)}
i_i   i 0   dc 0 ac {if(lgm==1,0,1)}
i_id  0 v4  dc 0 ac {if(lgm==1,0,1)}
h_1   i1 0  v_1 1
h_2   i2 0  v_2 1
r_1   i1 0  1
r_2   i2 0  1
.ends

% 180 Grad Hybrid
.subckt hybrid180 1 2 3 4 params: z0=50 f=1e6
.param l={sqrt(2)*z0/(2*3.1459*f)}
.param c={1/(sqrt(2)*z0*2*3.1459*f)}
c1   1 0  {2*c}
c2   2 0  {c}
c3   3 0  {2*c}
c4   4 0  {c}
c25  2 5  {c}
c45  4 5  {c}
l5   5 0  {l}
l13  1 3  {l}
l14  1 4  {l}
l23  2 3  {l}
.ends

% 90 Grad Hybrid
%.param l={z0/(2*3.1459*f*sqrt(1+2*3.1459*f*c1*z0))}
%.param c0={1/(pwr(2*3.1459*f,2)*l)-c1}
.subckt hybrid90 1 2 3 4 params: z0=50 f=1e6
.param c1={1/(2*3.1459*f*z0)}
.param l={z0/(2*3.1459*f*sqrt(2))}
.param c0={(sqrt(2)-1)/(2*3.1459*f*z0)}
c1   1 0  {c0}
c2   2 0  {c0}
c3   3 0  {c0}
c4   4 0  {c0}
c12  1 2  {c1}
c34  3 4  {c1}
l13  1 3  {l}
l24  2 4  {l}
.ends

% 90 Grad Hybrid mit Leitungen
.subckt hybrid90t 1 2 3 4 params: z0=50 f=1e6
.param c={1/(2*3.1459*f*z0)}
.param td={1/(8*f)}
t13  1 0 3 0  z0={z0} td={td}
t24  2 0 4 0  z0={z0} td={td}
c12  1 2      {c}
c34  3 4      {c}
.ends

% Zweikreisiges Filter
.subckt zk-filter 1 2 params: z0=50 f=1e6 b=1e5 k=1
.param fr={sqrt(f*f-b*b/4)}
.param q={1.412*k*fr/b}
.param l={z0/(q*2*3.14159*fr)}
.param ck={k/(z0*2*3.14159*fr)}
.param c={(q-k)/(z0*2*3.14159*fr)}
l1   1 0  {l}
c1   1 0  {c}
c12  1 2  {ck}
l2   2 0  {l}
c2   2 0  {c}
.ends
.subckt zk-filter-n 1 2 params: z0=50 f=1e6 b=1e5 k=1 n=0.5
.param fr={sqrt(f*f-b*b/4)}
.param q={1.412*k*fr/b}
.param z={n*n*z0}
.param l={z/(q*2*3.14159*fr)}
.param ck={k/(z*2*3.14159*fr)}
.param c={(q-k)/(z*2*3.14159*fr)}
l1   3 0  {l}
c1n  1 0  {n*c}
c1   1 3  {n*c/(n-1)}
c12  3 4  {ck}
l2   4 0  {l}
c2n  2 0  {n*c}
c2   2 4  {n*c/(n-1)}
.ends

% Cosinus-Rolloff-Filter
.subckt rcosflt03 1 2 params: z0=50 ts=1u d=0
.param ta={ts/4}
.param k={exp(-d/8.68)}
r1   1 0  {z0}
e1   3 0  laplace {v(1)*k}={0.0405+0.0315*exp(-ta*s)-0.0085*exp(-2*ta*s)
+ -0.0635*exp(-3*ta*s)-0.0579*exp(-4*ta*s)-0.0448*exp(-5*ta*s)+0.0087*exp(-6*ta*s)
+ +0.094*exp(-7*ta*s)+0.2282*exp(-8*ta*s)+0.2924*exp(-9*ta*s)+0.281*exp(-10*ta*s)
+ +0.1984*exp(-11*ta*s)+0.1238*exp(-12*ta*s)+0.0202*exp(-13*ta*s)-0.0562*exp(-14*ta*s)
+ -0.0878*exp(-15*ta*s)-0.0337*exp(-16*ta*s)+0.003*exp(-17*ta*s)+0.0201*exp(-18*ta*s)
+ +0.0107*exp(-19*ta*s)+0.0298*exp(-20*ta*s)}
r2   3 2  {z0}
.ends
.subckt rcosflt05 1 2 params: z0=50 ts=1u d=0
.param ta={ts/4}
.param k={exp(-d/8.68)}
r1   1 0  {z0}
e1   3 0  laplace {v(1)*k}={0.0049+0.0125*exp(-ta*s)+0.0056*exp(-2*ta*s)
+ -0.0229*exp(-3*ta*s)-0.0529*exp(-4*ta*s)-0.05*exp(-5*ta*s)+0.0032*exp(-6*ta*s)
+ +0.0998*exp(-7*ta*s)+0.2097*exp(-8*ta*s)+0.2877*exp(-9*ta*s)+0.2901*exp(-10*ta*s)
+ +0.2125*exp(-11*ta*s)+0.097*exp(-12*ta*s)+0.0007*exp(-13*ta*s)-0.0476*exp(-14*ta*s)
+ -0.0502*exp(-15*ta*s)-0.0257*exp(-16*ta*s)+0.0031*exp(-17*ta*s)+0.0149*exp(-18*ta*s)
+ +0.0077*exp(-19*ta*s)-0.0028*exp(-20*ta*s)}
r2   3 2  {z0}
.ends
.subckt rcosflt10 1 2 params: z0=50 ts=1u d=0
.param ta={ts/4}
.param k={exp(-d/8.68)}
r1   1 0  {z0}
e1   3 0  laplace {v(1)*k}={0.002-0.002*exp(-ta*s)-0.0037*exp(-2*ta*s)
+ +0.0037*exp(-3*ta*s)+0.01*exp(-4*ta*s)-0.0102*exp(-5*ta*s)-0.0279*exp(-6*ta*s)
+ +0.0281*exp(-7*ta*s)+0.18*exp(-8*ta*s)+0.3201*exp(-9*ta*s)+0.3207*exp(-10*ta*s)
+ +0.1792*exp(-11*ta*s)+0.0289*exp(-12*ta*s)-0.0286*exp(-13*ta*s)-0.0095*exp(-14*ta*s)
+ +0.0092*exp(-15*ta*s)+0.0045*exp(-16*ta*s)-0.0043*exp(-17*ta*s)-0.0014*exp(-18*ta*s)
+ +0.0012*exp(-19*ta*s)}
r2   3 2  {z0}
.ends

% Butterworth-Filter 5.Grades
.subckt butterworth5 1 2 params: z0=50 fg=1e6 d=0
.param t={1/(6.28*fg)}
.param k={exp(-d/8.68)}
r1   1 0  {z0}
e1   3 0  laplace {v(1)*k}={1/((1+s*t)*(1+1.618*s*t+s^2*t^2)*(1+0.618*s*t+s^2*t^2))}
r2   3 2  {z0}
.ends

% I/Q-Mischer
.subckt iqmischer 1 2 3 params: z0=50 ft=10e6 at=1 g=0 phi=0
.param k={exp(g/8.68)}
r1   1 0  {z0}
r2   2 0  {z0}
vcos c 0  dc 1 sin(0 {at} {ft} 0 0 {90+phi})
vsin s 0  dc 0 sin(0 {at} {ft} 0 0 {phi})
e1   4 0  value={k*(v(1)*v(c)-v(2)*v(s))}
r3   4 3  {z0}
.ends

% Mischer
.subckt mischer 1 2 3 params: z0=50 g=0
.param k={exp(g/8.68)}
r1   1 0  {z0}
r2   2 0  {z0}
e1   4 0  value={k*(v(1)*v(2))}
r3   4 3  {z0}
.ends

% Verzoegerungsglied
.subckt delay 1 2 params: z0=50 td=1u d=0
.param k={exp(-d/8.68)}
r1   1 0  {z0}
e1   3 0  laplace {v(1)*k}={exp(-s*td)}
r2   3 2  {z0}
.ends

% Schmitt-Trigger
.subckt trigger 1 2 params: z0=50 trg=0 hyst=1 uamax=5 uamin=0 td=10n
.param hub={uamax-uamin}
.param mitte={(uamax-uamin)/2}
r1   1 0  {z0}
e1   3 0  value={limit(v(1,5)*1e6,uamin,uamax)}
r2   3 2  {z0}
e2   4 0  value={trg-(v(3)-mitte)/hub*hyst}
r3   4 5  1
c    0 5  {td/3}
.ends

% HF-Ersatzschaltbilder
.subckt rhf 1 2 params: r=50 cr=0.2p lr=3n
r1  1 3  {r}
l1  2 3  {lr}
c1  1 2  {cr}
.ends
.subckt chf 1 2 params: c=1n lc=3n rc=0.2
c1  1 3  {c}
l1  3 4  {lc}
r1  4 2  {rc}
rd1 1 2  1e12
.ends
.subckt lhf 1 2 params: l=100n cl=0.2p krl=1.2e-4
l1  1 3  {l}
g1  2 3  laplace { v(2,3) } = { 1/(krl*sqrt(1+0.31831*s)) }
c1  1 2  {cl}
rd1 1 2  1e12
.ends

% Schalter
.subckt schalter 1 2 3 params: ron=1 roff=1e12 schwelle=0 inv=0
.param voff={schwelle-0.01}
.param von={schwelle+0.01}
rd1 3 0      1e12
rd2 4 0      1e12
e1  4 0      value={2*inv*schwelle+(1-2*inv)*v(3)}
s1  1 2 4 0  smod
.model smod vswitch (ron={ron} roff={roff} von={von} voff={voff})
.ends
