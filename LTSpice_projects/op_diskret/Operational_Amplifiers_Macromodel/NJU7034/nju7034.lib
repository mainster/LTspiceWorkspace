*$
*PART NUMBER: NJU7034
*MANUFACTURER: NEW JAPAN RADIO 
*CMOS OPAMP
*All Rights Reserved Copyright (c) Bee Technologies Corporation 2004
.SUBCKT nju7034 IN-1 IN+1 IN-2 IN+2 VDD VSS OUT1 OUT2
+ IN-3 IN+3 IN-4 IN+4 OUT3 OUT4
X_U1 IN-1 IN+1 VDD VSS OUT1 nju7034_s
X_U2 IN-2 IN+2 VDD VSS OUT2 nju7034_s
X_U3 IN-3 IN+3 VDD VSS OUT3 nju7034_s
X_U4 IN-4 IN+4 VDD VSS OUT4 nju7034_s
.ENDS nju7034
*$
.SUBCKT nju7034_s   IN- IN+ VDD VSS OUT
M1         2 IN- 3 VDD MbreakPD3  
M2         2 IN+ 4 VDD MbreakPD2           
M3         VDD 1 2 VDD MbreakPD           
M4         VDD 1 5 VDD MbreakPD           
M5         VDD 1 6 VDD MbreakPD         
M6         VDD 1 1 VDD MbreakPD           
M7         5 5 VSS VSS MbreakND  W=3.2m   L=6u
M8         5 4 VSS VSS MbreakND3             
M9         3 3 IN1 VSS MbreakND1   
M10        4 3 IN2 VSS MbreakND1          
M11        1 6 11 11 MbreakND     W=9m   L=6u
M12        6 6 VSS VSS MbreakND3  
M13        7 5 VSS VSS MbreakND1     
M14        VDD 7 7 VDD MbreakPD           
M15        VDD 7 OUT VDD MbreakPD1          
M16        OUT 4 VSS VSS MbreakND2
C1         OUT IN- 10p
C2         OUT 1 0.9p
C3         OUT 3 45p
R1         11 VSS  1.522k          
R2         IN1 VSS 2.0k                  
R3         IN2 VSS  3.93k  
I1         0 IN-  0.505p  
I2         0 IN+  1.5p  
X_U1       VSS 3 DbreakZ 
X_U2       VSS 4 DbreakZ
.model MbreakND NMOS (LEVEL=3  VTO=0.9 RS=10.000E-3
+ RD=10.000E-3 RDS=1.0000E6 TOX=2.0000E-6 CGSO=4.000E-12
+ CGDO=1.000E-12 CBD=1.000E-12 RG=5 RB=1.0000E-3 KP=10E-6)
.model MbreakND1 NMOS (LEVEL=3 L=6u W=0.5 VTO=1
+ RS=10.000E-3 RD=10.000E-3 RDS=1.0000E6 TOX=2.0000E-6
+ CGSO=1.00E-10 CGDO=1.000E-11 CBD=1.000E-10 RG=5
+ RB=1.0000E-3 KP=10E-6)
.model MbreakND2 NMOS (LEVEL=3 L=6u W=0.483m VTO=0.9
+ RS=10.000E-3 RD=10.000E-3 RDS=1.0000E6 TOX=2.0000E-6
+ CGSO=4.000E-12 CGDO=1.00E-12 CBD=1.000E-12 RG=5
+ RB=1.0000E-3 KP=10E-6)
.model MbreakND3 NMOS (LEVEL=3  L=6u  W=3.2m  VTO=0.9
+ RS=10.000E-3 RD=10.000E-3 RDS=1.0000E6 TOX=2.0000E-6
+ CGSO=1.000E-12 CGDO=1.000E-12 CBD=1.000E-12
+ RG=5 RB=1.0000E-3 KP=10E-6)
.model MbreakPD PMOS (LEVEL=3 L=6u W=0.023 VTO=-1 RS=10.000E-3
+ RD=10.000E-3 RDS=1.0000E6 TOX=2.0000E-6 CGSO=4.000E-12
+ CGDO=1.000E-12 CBD=1.000E-12 RG=5 RB=1.0000E-3 KP=1E-6)
.MODEL MbreakPD1 PMOS (LEVEL=3 L=6u W=0.0085 VTO=-0.9
+ RS=10.000E-3 RD=10.000E-3 RDS=1.00E6 TOX=2.0000E-6
+ CGSO=4.000E-12 CGDO=1.000E-12 CBD=1.000E-12
+ RG=5 RB=1.0000E-3 KP=1E-6)
.MODEL MbreakPD2 PMOS (LEVEL=3 L=6u W=0.05 VTO=-1.4
+ RS=10.000E-3 RD=10.00E-3 RDS=1.2500E6 TOX=2.0000E-6
+ CGSO=4.000E-12 CGDO=1.000E-12 CBD=1.00E-12
+ RG=5 RB=1.0000E-3 KP=1E-6)
.MODEL MbreakPD3 PMOS (LEVEL=3 L=6u W=0.0719 VTO=-1.4
+ RS=10.000E-3 RD=10.00E-3 RDS=1.E6 TOX=2.0000E-6
+ CGSO=4.000E-12 CGDO=1.000E-12 CBD=1.00E-12
+ RG=5 RB=1.0000E-3 KP=1E-6)
.ENDS nju7034_s 
*$
.SUBCKT DbreakZ  A K
D1 A K DF
DZ A2 A DR
VZ K A2 1
.MODEL DF D 
.MODEL DR D 
.ENDS DbreakZ
*$