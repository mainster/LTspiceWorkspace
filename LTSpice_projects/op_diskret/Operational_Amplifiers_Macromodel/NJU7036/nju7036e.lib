*$
*PART NUMBER: NJU7036E
*MANUFACTURER: NEW JAPAN RADIO
*CMOS OPAMP
*All Rights Reserved Copyright (C) Bee Technologies Corporation 2009
.SUBCKT NJU7036E 1 2 3 4 5 6 7 8 9 10 11 12 13 14
X_UA	6 7 1 4 2	NJU7036E_SUB
X_UB	9 8 14 10 12  	NJU7036E_SUB
R_Rss1  4 5		0.1u
R_Rss2  10 11		0.1u
R_Rdd   1 14		0.1u
R_ROUTA 2 3		0.1u
R_ROUTB 12 13		0.1u
.ENDS
*$
.SUBCKT NJU7036E_SUB INPUT- INPUT+ VDD VSS OUTPUT
M_M1      3 INPUT- 2 VDD MbreakPD3   L=6u W=8.5m
M_M2      4 INPUT+ 2 VDD MbreakPD2   L=6u W=8.5m
M_M3      2 1 VDD VDD MbreakPD
M_M4      5 1 VDD VDD MbreakPD
M_M5      6 1 VDD VDD MbreakPD
M_M6      1 1 VDD VDD MbreakPD
M_M7      5 5 VSS VSS MbreakND
M_M8      5 4 VSS VSS MbreakND
M_M9      3 3 VSS VSS MbreakND1      L=6u W=100m
M_M10     4 3 VSS VSS MbreakND1      L=6u W=100m
M_M11     1 6 8 8 MbreakND
M_M12     6 6 VSS VSS MbreakND
M_M13     7 5 VSS VSS MbreakND1
M_M14     7 7 VDD VDD MbreakPD
M_M15     OUTPUT 7 10 10 MbreakPD1   L=6u W=6 
M_M16     9 4 VSS VSS MbreakND2      L=6u W=15
V_V1      OUTPUT 9 0.1412
V_V2      VDD 10 0.0563
R_R1      8 VSS  10  
R_R2      INPUT+ VSS  1E12
R_R3      INPUT- VSS  3E12
R_R4      VDD VSS     3.1976k
C_C1      OUTPUT 4    525p
.model MbreakND NMOS ( LEVEL=3 L=6u W=5m VTO=0 RS=10.000E-3 
+ RD=10.000E-3 RDS=1E6 TOX=2.0E-6 RG=5 RB=1.0000E-3
+ KP=5.1E-6)
.model MbreakND1 NMOS (LEVEL=3 L=6u W=5m VTO=0 RS=10.000E-3
+ RD=10.000E-3 RDS=1.2E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3 
+ KP=10E-6)
.model MbreakND2 NMOS ( LEVEL=3 VTO=0 RS=10.000E-3 RD=10.000E-3 
+ RDS=1.0000E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3
+ CBD=50E-8 KP=10E-6)
.model MbreakPD PMOS ( LEVEL=3 L=6u W=5m VTO=0 RS=10.000E-3
+ RD=10.00E-3 RDS=1.00E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3  
+ KP=10E-6 )
.MODEL MbreakPD1 PMOS (LEVEL=3 VTO=0 RS=10.000E-3 RD=10.000E-3 
+ RDS=1.00E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3 KP=10E-6 )
.MODEL MbreakPD2 PMOS (LEVEL=3 VTO=0 RS=10.000E-3 RD=10.00E-3 
+ RDS=1.0E6 TOX=2.0000E-6 RG=5 RB=1.000E-3 KP=10E-6)
.MODEL MbreakPD3 PMOS (LEVEL=3 VTO=-2.12m RS=10.000E-3 RD=10.00E-3 
+ RDS=1.0E6 TOX=2.000E-6 RG=5 RB=1.000E-3 KP=10E-6 )
.ENDS NJU7036E_SUB 
*$
