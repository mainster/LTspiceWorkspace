*$
*PART NUMBER: NJU7029
*MANUFACTURER: NEW JAPAN RADIO
*CMOS OPAMP
*All Rights Reserved Copyright (C) Bee Technologies Corporation 2009
.SUBCKT NJU7029 1 2 3 4 5 6 7 8
X_U1	3 4 2 1 8	NJU7029_SUB 	
X_U2	5 4 6 7 8	NJU7029_SUB
.ENDS
*$
.SUBCKT NJU7029_SUB INPUT+ VSS INPUT- OUTPUT VDD
M_M1 	3 INPUT- 2 VDD	MbreakPD3 	L=6u W=2.5m
M_M2    4 INPUT+ 2 VDD 	MbreakPD2	L=6u W=2.5m
M_M3    2 1 VDD VDD 	MbreakPD
M_M4    5 1 VDD VDD 	MbreakPD
M_M5    6 1 VDD VDD 	MbreakPD
M_M6    1 1 VDD VDD 	MbreakPD
M_M7    5 5 VSS VSS 	MbreakND
M_M8    5 4 VSS VSS 	MbreakND
M_M9    3 3 8 VSS 	MbreakND1 	L=6u W=65m
M_M10   4 3 9 VSS 	MbreakND1 	L=6u W=85m
M_M11   1 6 10 10 	MbreakND 
M_M12   6 6 VSS VSS 	MbreakND
M_M13   7 5 VSS VSS 	MbreakND1
M_M14   7 7 VDD VDD 	MbreakPD 
M_M15   OUTPUT 7 12 12 	MbreakPD1	L=2u W=50m
M_M16   11 4 VSS VSS 	MbreakND2	L=2u W=15m
V_V1    OUTPUT 11 	0.089
V_V2    VDD 12  	0.099
R_R1    10 VSS  	200k
R_R2    8 VSS   	200
R_R3    9 VSS   	200
R_R4    INPUT+ VSS 	1.0E+12
R_R5    INPUT- VSS 	3.0E+12
R_R6    VDD VSS 	10.123E+3
C_C1    OUTPUT 4 	0.415p
.model MbreakND NMOS ( LEVEL=3 L=6u W=128u VTO=0 RS=10.000E-3 
+ RD=10.000E-3 RDS=10E6 TOX=2.0E-6 RG=5 RB=1.0000E-3
+ KP=10E-6)
.model MbreakND1 NMOS (LEVEL=3 L=6u W=200u VTO=0 RS=10.000E-3
+ RD=10.000E-3 RDS=2.4E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3 
+ KP=10E-6)
.model MbreakND2 NMOS ( LEVEL=3 VTO=0 RS=10.000E-3 RD=10.000E-3 
+ RDS=1.0000E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3  
+ KP=10E-6)
.model MbreakPD PMOS ( LEVEL=3 L=6u W=27u VTO=0 RS=10.000E-3
+ RD=10.00E-3 RDS=1.00E9 TOX=2.0000E-6 RG=5 RB=1.0000E-3  
+ KP=10E-6 )
.MODEL MbreakPD1 PMOS (LEVEL=3 VTO=-0.1 RS=10.000E-3 RD=10.000E-3 
+ RDS=1.00E6 TOX=2.0000E-6 RG=5 RB=1.0000E-3 KP=50E-6 )
.MODEL MbreakPD2 PMOS (LEVEL=3 VTO=0 RS=10.000E-3 RD=10.00E-3 
+ RDS=5E9 TOX=2.0000E-6 RG=5 RB=1.000E-3 KP=1E-6)
.MODEL MbreakPD3 PMOS (LEVEL=3 VTO=-5.25m RS=10.000E-3 RD=10.00E-3 
+ RDS=5E9 TOX=2.000E-6 RG=5 RB=1.000E-3 KP=1E-6 )
.ENDS NJU7029_SUB 
*$
